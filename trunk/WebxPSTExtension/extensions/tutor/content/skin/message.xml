<?xml version="1.0" encoding="ISO-8859-1"?>
<!--
Copyright (c) Clearsighted 2006-08 stephen@clearsighted.net

This library is free software; you can redistribute it and/or
modify it under the terms of the GNU Lesser General Public
License as published by the Free Software Foundation; either
version 2.1 of the License, or (at your option) any later version.

This library is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
Lesser General Public License for more details.

You should have received a copy of the GNU Lesser General Public
License along with this library; if not, write to the Free Software
Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA  02110-1301  USA
-->

<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform">
	<xsl:output method="xml"/>
	<xsl:template match="/message">
		<html xmlns="http://www.w3.org/1999/xhtml">
			<head>
				<base>
					<xsl:attribute name="href"><xsl:value-of select="base/@href"/></xsl:attribute>
				</base>
				<!-- Include the stylesheet -->
				<link rel="stylesheet" href="{skinbase/@href}/styles.css" type="text/css"/>
				<xsl:if test="not(isimage)">
					<script type="text/javascript">
						var g_base = '<xsl:value-of select="base/@href"/>';
						var g_skinbase = '<xsl:value-of select="skinbase/@href"/>';
					</script>
					<!-- these xsl:texts are weird, right? But they're necessary because Firefox wants a begin-end tag for script rather than a single closed tag, and XSLT will coalesce the former into the latter if there's no text in there -->
					<script type="text/javascript" src="{skinbase/@href}../sarissa/sarissa.js"><xsl:text> </xsl:text></script>
					<script type="text/javascript" src="{skinbase/@href}../sarissa/sarissa_ieemu_xpath.js"><xsl:text> </xsl:text></script>
					<script type="text/javascript" src="{skinbase/@href}../jquery/jquery.js"><xsl:text> </xsl:text></script>
					<script type="text/javascript" src="{skinbase/@href}../jquery/jquery.xslTransform.js"><xsl:text> </xsl:text></script>
					<script type="text/javascript" src="{skinbase/@href}message.js"><xsl:text> </xsl:text></script>
					<script type="text/javascript" src="{skinbase/@href}util.js"><xsl:text> </xsl:text></script>
				</xsl:if>
			</head>

<!-- START <body> PROCESSING -->
<!-- ======================= -->
			
			<xsl:if test="isimage">
	 			<body class="special">
					<div id="body">
					<img width="100%" height="100%">
						<xsl:attribute name="src"><xsl:value-of select="url/text()"/></xsl:attribute>
					</img>
					</div>
				</body>
			</xsl:if>

			<xsl:if test="isjit">
				<body class="JIT">
					<div id="body">
						<div id="hint">
							<xsl:call-template name="maintext"/>
							
							<p></p>
							<p></p>
						</div>
						<div id="button">
							<a href="javascript:window.parent.close()" onmouseover="rollover('example', image08)" onmouseout="rollover('example', image07)">
								<img class="blockcentered" src="{skinbase/@href}/images/ok.png" name="example"/>
							</a>
						</div>
					</div>
				</body>
			</xsl:if>

			<xsl:if test="ishint">
				<body class="special">
				<div id="header" onmousedown="mousedown(event)" onmouseup="mouseup(event)" onmousemove="mousemove(event)">
					<div id="headergreen" >
						<div id="redx">
							 <a href="javascript:window.parent.close()"><img src="{skinbase/@href}/images/redx.png"/></a>
						</div>
						<img src="{skinbase/@href}/images/header_hint.gif"/>
					</div>
				</div>
				
				<div id="body">
					<div id="hint">
						<xsl:call-template name="maintext"/>
						
						<p></p>
						<p></p>
					</div>
				<div id="button">
					<a href="javascript:window.parent.close()" onmouseover="rollover('example', image02)" onmouseout="rollover('example', image01)">
						<img src="{skinbase/@href}/images/close.jpg" name="example"/>
					</a>
					
					<img src="{skinbase/@href}/images/button_space.jpg"/>

					<xsl:choose>
						<xsl:when test="page/@index &gt; 0">
							<a href="javascript:window.parent.prevHint()" onmouseover="rollover('example2', image04)" onmouseout="rollover('example2', image03)">
								<img src="{skinbase/@href}/images/previoushint.jpg" name="example2"/>
							</a>
						</xsl:when>
						<xsl:otherwise>
							<img src="{skinbase/@href}/images/previoushint_gray.jpg" name="example2"/>
						</xsl:otherwise>
					</xsl:choose>
					
					<xsl:choose>
						<xsl:when test="page/@index &lt; page/@total - 1">
							<a href="javascript:window.parent.nextHint()" onmouseover="rollover('example3', image06)" onmouseout="rollover('example3', image05)">
								<img src="{skinbase/@href}/images/nexthint.jpg" name="example3"/>
							</a>
						</xsl:when>
						<xsl:otherwise>
							<img src="{skinbase/@href}/images/nexthint_gray.jpg" name="example3"/>
						</xsl:otherwise>
					</xsl:choose>
				 </div>
				</div>
				</body>
			</xsl:if>

			<xsl:if test="iswtp">
				<body class="special">

				<div id="header" onmousedown="mousedown(event)" onmouseup="mouseup(event)" onmousemove="mousemove(event)">
					<div id="headergray">
						<div id="redx">
							 <a href="javascript:window.parent.close()"><img src="{skinbase/@href}/images/redx.png"/></a>
						</div>
						
						<img src="{skinbase/@href}/images/header_whatsthepoint.gif"/>
					</div>
				</div>
				<div id="body">
					<div id="wtp-style">
						<xsl:call-template name="maintext"/>
						
						<p></p>
						<p></p>
					</div>
				<div id="button">
					<a href="javascript:window.parent.close()" onmouseover="rollover('example', image02)" onmouseout="rollover('example', image01)">
						<img src="{skinbase/@href}/images/close.jpg" name="example"/>
					</a>
				 </div>
				</div>
				</body>
			</xsl:if>

			<xsl:if test="isconcepts">
				<body class="special">

				<div id="header" onmousedown="mousedown(event)" onmouseup="mouseup(event)" onmousemove="mousemove(event)">
					<div id="headergreen">
						<div id="redx">
							 <a href="javascript:window.parent.close()"><img src="{skinbase/@href}/images/redx.png"/></a>
						</div>
						
						<img src="{skinbase/@href}/images/header_concepts.gif"/>
					</div>
				</div>
				<div id="body">
					<div id="concepts-style">
						<xsl:call-template name="maintext"/>
						
						<p></p>
						<p></p>
					</div>
				<div id="button">
					<a href="javascript:window.parent.close()" onmouseover="rollover('example', image02)" onmouseout="rollover('example', image01)">
						<img src="{skinbase/@href}/images/close.jpg" name="example"/>
					</a>
				 </div>
				</div>
				</body>
			</xsl:if>

			<xsl:if test="isscenario">
				<body class="special">
					<div id="body">
						<xsl:call-template name="maintext"/>
					</div>
				</body>
			</xsl:if>
		</html>
	</xsl:template>

	<xsl:template name="maintext">
		<xsl:choose>
			<xsl:when test="p">
				<xsl:apply-templates mode="maintext"/>
			</xsl:when>
			<xsl:otherwise>
				<p><xsl:apply-templates mode="maintext"/></p>
			</xsl:otherwise>
		</xsl:choose>
	</xsl:template>
	
	<xsl:template mode="maintext" match="text()">
		<xsl:value-of select="."/>
	</xsl:template>

	<xsl:template mode="maintext" match="apopup">
		<a href="#">
			<xsl:attribute name="onclick">popup(event, '<xsl:value-of select="@type"/>', '<xsl:value-of select="@href"/>', this)</xsl:attribute>
			<xsl:apply-templates mode="maintext"/>
		</a>
	</xsl:template>
	
	<xsl:template mode="maintext" match="image">
		<xsl:choose>
			<xsl:when test="@thumbnail">
				<img onmouseout="popdownimage()">
					<xsl:if test="@class">
						<xsl:attribute name="class"><xsl:value-of select="@class"/></xsl:attribute>
					</xsl:if>
					<xsl:attribute name="src"><xsl:value-of select="@thumbnail"/></xsl:attribute>
					<xsl:attribute name="onmouseover">popupimage('<xsl:value-of select="@src"/>', this)</xsl:attribute>
				</img>
			</xsl:when>
			<xsl:otherwise>
				<img>
					<xsl:if test="@class">
						<xsl:attribute name="class"><xsl:value-of select="@class"/></xsl:attribute>
					</xsl:if>
					<xsl:attribute name="src"><xsl:value-of select="@src"/></xsl:attribute>
				</img>
			</xsl:otherwise>
		</xsl:choose>
	</xsl:template>

	<!-- for all the XHTML tags that we allow that occur inline, just copy them in -->
	<xsl:template mode="maintext" match="p|em|strong|h1|h2|h3|ul|ol|li|br|hr|sup|sub">
		<xsl:copy>
			<xsl:apply-templates mode="maintext"/>
		</xsl:copy>
	</xsl:template>
</xsl:stylesheet>

